#!/usr/bin/env perl
use warnings;
use strict;
use utf8;
binmode(STDOUT, ":utf8");
use LWP::Simple qw/get/;
use Getopt::Long;
use Pod::Usage;
use JSON;
use Term::ANSIColor qw/:constants/;
use File::Slurp qw/slurp/;
use REST::Google::Translate;
use I18N::Langinfo qw(langinfo CODESET);
my $codeset = langinfo(CODESET);
use Encode qw(decode);
@ARGV = map { decode $codeset, $_ } @ARGV;
use constant {
	DAUM_ENDIC_URL => "http://apis.daum.net/dic/endic?apikey=%s&kind=WORD&output=json&q=%s"
};

our $VERSION = '1.00';

my %options;
GetOptions(\%options, "--file=s", "--help");

run(\%options, @ARGV);

sub run {
    my($opts, @words) = @_;

    if ($opts->{help}) {
        pod2usage(0);
    }

	if ($opts->{file}) {
		unshift @words, scalar slurp($opts->{file});
	}

	if (@words == 0) {
		push @words, join '', <STDIN>;
	}

	for my $word (@words) {
		local $Term::ANSIColor::AUTORESET = 1;
		print BOLD BLUE $word, "\n";

		if ($word =~ m/\w+ \w+/) {
			REST::Google::Translate->http_referer('http://localhost');
			my $res = REST::Google::Translate->new(
				q => $word,
				langpair => 'en|ko'
			);

			die "response status failure" if $res->responseStatus != 200;
			my $translated = $res->responseData->translatedText;
			print "\t$translated\n";
			next;
		}

		my $result = from_json(get_daum($word));
		for my $word (@{ $result->{channel}->{item} }) {
			print "$word->{title}\n";
			print "\t$word->{description}\n";
		}
	}
}

sub get_daum {
    my $word = shift;
	unless ($ENV{DAUM_ENDIC_KEY}) {
		$ENV{DAUM_ENDIC_KEY} = 'DAUM_DIC_DEMO_APIKEY';
	}
	my $url = sprintf(DAUM_ENDIC_URL, $ENV{DAUM_ENDIC_KEY}, $word);
	return get($url);
}

__END__

=head1 NAME

eng2kor - English to Korean
Dictionary Service

recommend - request a english dictionary API key to Daum and using it.

=head1 VERSION

Version $VERSION

=head1 SYNOPSIS

	eng2kor --help

	eng2kor "word"
	eng2kor 단어                                                 	    # kor2eng
	eng2kor some thing "something"                                      # multiple search
	eng2kor "this is sentence"                                          # sentence
	echo "word" | eng2kor                                               # pipe input
	eng2kor --file=eng.txt                                              # file translate
	export DAUM_ENDIC_KEY=e4208a9e48744c40f2b7459162062313ed9878f6      # note: just sample, invalid key

=head1 INSTALL

	perl Makefile.PL
	make
	make test
	make install

=head1 AUTHOR

hshong, C<< <hshong at perl.kr> >>

=head1 LICENSE AND COPYRIGHT

same as Perl.

=head1 SEE ALSO

L<http://dna.daum.net/griffin/do/DevDocs/read?bbsId=DevDocs&articleId=11>

=cut

1; # End of eng2kor
